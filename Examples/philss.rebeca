reactiveclass Philosopher()
{
	knownrebecs
	{
		Fork forkL;
		Fork forkR;
	}
	statevars
	{
		boolean eating;
		boolean fL;
		boolean fR;
	}
	msgsrv initial(boolean starter)
	{
		fL = false;
		fR = false;
		eating = false;
		self.arrive();
	}

	msgsrv arrive()
	{
		forkL.request(self);
	}

	msgsrv permit(int s)
	{
		if (s == forkL) {
			if (!fL) {
				fL = true;
				forkR.request(self);
			}
		}
		else {
			if (fL && !(fR)) {
				fR = true;
				self.eat();
			}
			/* else discard the message */
		}
	}

	msgsrv eat()
	{
		eating = true;
		self.leave();
	}

	msgsrv leave()
	{
		fL = false;
		fR = false;
		eating = false;
		forkL.release(self);
		forkR.release(self);
		self.arrive();
	}
}

reactiveclass Fork()
{
	knownrebecs
	{
		Philosopher philL;
		Philosopher philR;
	}
	statevars
	{
		boolean lAssign;	
		boolean rAssign;	
		boolean leftReq;
		boolean rightReq;
	}
	msgsrv initial(boolean starter)
	{ 
		lAssign = false;
		rAssign = false;
		leftReq = false;
		rightReq = false;
	}

	msgsrv request(int s)
	{
		if (s == philL) {
			if (!leftReq) {
				leftReq = true;
				if (!rAssign) {
					lAssign = true;
					philL.permit(self);
				}
			} 
			/* else discard message */
		}
		else {
			if (!rightReq) {
				rightReq = true;
				if (!lAssign) {
					rAssign = true;
					philR.permit(self);
				}
			}
			/* else discard message */
		}
	}
	msgsrv release(int s)
	{
		if (s == philL && lAssign){
			leftReq = false;
			lAssign = false;
			if (rightReq) {
				rAssign=true;
				philR.permit(self);
			}
		}
		if (s == philR && rAssign){
			rAssign = false;
			rightReq = false;
			if (leftReq) {
				lAssign=true;
				philL.permit(self);
			}
		}
	}
}

main
{
       Philosopher phil0(fork0, fork2):(false);
       Philosopher phil1(fork0, fork1):(false);
       Philosopher phil2(fork1, fork2):(false);

       Fork fork0(phil0, phil1):(false);
       Fork fork1(phil1, phil2):(false);
       Fork fork2(phil2, phil0):(false);
}
